[private]
default:
    @just --list

create_day day:
    #!/usr/bin/bash
    set -eou pipefail
    if [[ ! "{{ day }}" =~ ^[0-2][0-9]$ ]]; then
        echo "Improper Day..."
        exit 1
    fi
    mkdir -p src/{{ day }}
    cat > src/{{ day }}/main.c << 'EOF'
    #define AOC_UTILS_IMPLEMENTATION
    #include "../../lib/aoc.h"

    #define DELIM " \n"

    int main(int argc, char **argv)
    {
        if (argc < 2)
        {
            printf("Missing file\n");
            return EXIT_FAILURE;
        }

        char *filename = argv[1];
        size_t solution = 0;

        // Read in File
        char *buffer = read_file(filename);
        size_t lines = count_lines(filename);

        // Part 1
        printf("Part1 Solution: %d\n", solution);

        // Part 2
        solution = 0;
        printf("Part2 Solution: %d\n", solution);

        if (buffer)
            free(buffer);

        return EXIT_SUCCESS;
    }
    EOF
    touch src/{{ day }}/test_file.txt
    touch src/{{ day }}/input_file.txt

build day:
    #!/usr/bin/bash
    set -eou pipefail
    if [[ ! "{{ day }}" =~ ^[0-2][0-9]$ ]]; then
        echo "Improper Day?"
        exit 1
    fi
    set -x
    mkdir -p build
    gcc -o {{ justfile_dir() }}/build/{{ day }}.binary {{ justfile_dir() }}/src/{{ day }}/main.c

run day: (build day)
    # Test File
    @./build/{{ day }}.binary "./src/{{ day }}/test_file.txt"
    # Input File
    @./build/{{ day }}.binary "./src/{{ day }}/input_file.txt"

